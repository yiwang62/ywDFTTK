#!/bin/tcsh
#
set www = "`pwd`"
set h = $www:t
unalias rm
unalias cp
unalias mv
set oszicar = "OSZICAR"
set contcar = "CONTCAR"
set doscar = ""
set outcar = ""
set workdir = `qqcd`
set ethr = 5000
set lthr = 5000

foreach o ( $* )
  if (x$1 == "xDOSCAR") then
    set doscar = "DOSCAR"
    if ( "$#argv" != 0 ) shift
    continue
  else if (x$1 == "xOUTCAR") then
    set outcar = "OUTCAR"
    if ( "$#argv" != 0 ) shift
    continue
  else if (x$1 == "x-workdir") then
    shift
    set workdir = "$1"
    if ( "$#argv" != 0 ) shift
    continue
  else if (x$1 == "x-ethr") then
    shift
    set ethr = "$1"
    if ( "$#argv" != 0 ) shift
    continue
  else if (x$1 == "x-lthr") then
    shift
    set lthr = "$1"
    if ( "$#argv" != 0 ) shift
    continue
  else
    if ( "$1" != "" ) set workdir = "$1"
    if ( "$#argv" != 0 ) shift
  endif
end

if ( ! -e $oszicar ) exit
if ( ! -e $contcar ) exit
if ( ! -e OUTCAR ) exit
if ( -z $oszicar ) exit
if ( -z $contcar ) exit
if ( -z OUTCAR ) exit

set nl = `wc -l <$contcar`
if ( "$nl" <= 9 ) exit

set dif = `stat -c %Y $oszicar`
if ( -e $workdir/OSZICAR ) then
  set t1 = `stat -c %Y $workdir/OSZICAR`
  @ dif =  "$t1" - "$dif"
else
  set dif = -1
endif
if ( "$dif" > 0 ) exit

set line = `grep "E0=" -a $oszicar -n | tail -1`
if ( "$line" == "" ) exit

set n1 = `echo $line | sed 's/:/ /g' | awk '{print $1}'`
@ n1 = "$n1" - 1
set line1 = `head -$n1 $oszicar | tail -1`
set ddE = `echo $line1 | awk '{print $4}'`
set dE = `ari -n -f %.0f $ddE x  $ethr | tr -d '-'`
if ( "$dE" != 0 ) exit

set ibzkpt =  "IBZKPT"
set nk = 1
if ( -e $ibzkpt ) then
if ( ! -z $ibzkpt ) then
  set line = `grep "Tetrahedra" $ibzkpt -n | tail -1`
  if ( "$line" != "" ) then
    set n1 = `echo $line | sed 's/:/ /g' | awk '{print $1}'`
    set nk = `head -$n1 $ibzkpt | awk '{print $4}' | awk '{total += $0} END{print total}'`
  else
    set nk = `awk '{print $4}' $ibzkpt | awk '{total += $0} END{print total}'`
  endif
  set natom = `head -7 POSCAR | tail -1 | awk '{ nn=0; for (i=1; i<=NF; i++) nn += $i; print nn;}'`
  @ nk = "$nk" * "$natom"
endif
endif
#cat <<end.end
  cp $contcar $workdir/.
  cp $oszicar $workdir/.

  set enmax = `grep ENMAX OUTCAR | tr -d ';' | sort -k3 -n | tail -1 | awk '{print $3}' | sort -n | tail -1`
  set encut = `grep ENCUT OUTCAR | tr -d ';' | awk '{print $3}' | sort -n | tail -1`
  set ismear = `grep SMEAR OUTCAR | tr -d ';' | awk '{print $3}' | sort -n | tail -1`

if ( -e $workdir/../tplate/INCAR.Static ) then
  set en0 = `grep ENCUT $workdir/../tplate/INCAR.Static | sed "s/ENCUT//;s/=//"`
  set high = `ari -n -f %.0f $encut - $en0 / $enmax x 100 +25.01`
else
  set high = `ari -n -f %.0f $encut - $enmax / $enmax x 100`
endif

if ( ("$high" >= "25") && ("$ismear" == "-5") ) then
  cp OUTCAR $workdir/.
  cp DOSCAR $workdir/.
  echo "StaticConvergent $nk $NERSC_HOST" > $workdir/StaticConvergent
else
  if ( "$outcar" != "" ) cp $outcar $workdir/.
  if ( "$doscar" != "" ) cp $doscar $workdir/.
  set line = `grep "E0=" -n $oszicar | tail -1`
  if ( "$line" != "" ) then
    set ddE = `echo $line | awk '{print $9}' | sed "s/=//g"`
    set dE = `ari -n -f %.0f $ddE x  $lthr | tr -d '-'`
    if ("$dE" == 0 ) then
      echo "RelaxConvergent $nk $NERSC_HOST" > $workdir/RelaxConvergent
    else
      if ( -e $workdir/RelaxConvergent ) rm $workdir/RelaxConvergent
    endif
  endif
endif

#ene.end
