#!/bin/tcsh
#
set www = "`pwd`"
set h = $www:t
unalias rm
unalias cp
unalias mv
set oszicar = "OSZICAR"
set contcar = "CONTCAR"
set workdir = "./"
set lthr = 10
set ethr = 5000

foreach o ( $* )
  if (x$1 == "x-workdir") then
    shift
    set workdir = "$1"
    if ( "$#argv" != 0 ) shift
    continue
  else if (x$1 == "x-CONTCAR") then
    shift
    set contcar = "$1"
    if ( "$#argv" != 0 ) shift
    continue
  else if (x$1 == "x-OSZICAR") then
    shift
    set oszicar = "$1"
    if ( "$#argv" != 0 ) shift
    continue
  else if (x$1 == "x-OUTCAR") then
    shift
    set outcar = "$1"
    if ( "$#argv" != 0 ) shift
    continue
  else if (x$1 == "x-lthr") then
    shift
    set lthr = "$1"
    if ( "$#argv" != 0 ) shift
    continue
  else if (x$1 == "x-ethr") then
    shift
    set ethr = "$1"
    if ( "$#argv" != 0 ) shift
    continue
  else
    if ( "$#argv" != 0 ) shift
  endif
end

if ( ! -e $oszicar ) goto out0
if ( ! -e $contcar ) goto out0
if ( -z $oszicar ) goto out0
if ( -z $contcar ) goto out0
set nl = `wc -l <$contcar`
if ( "$nl" <= 9 ) goto out0

set line = `grep "E0=" -a $oszicar -n | tail -1`
if ( "$line" == "" ) goto out0

set ddE = `echo $line | awk '{print $9}' | sed "s/=//g"`
set dE = `ari -n -f %.0f $ddE x  $lthr | tr -d '-'`

set n1 = `echo $line | awk '{print $1}' | sed 's/://g'`
if ( "$dE" != 0 ) then
  set n3 = `grep "E0=" -a $oszicar | wc -l`
  if ( "$n3" != 1 ) goto out0
  set n2 = `wc -l <$oszicar`
  if ( "$n1" != "$n2" ) goto out0
endif

@ n1 = "$n1" - 1
set line1 = "`head -$n1 $oszicar | tail -1`"
set ddE = `echo $line1 | awk '{print $4}'`
set dE = `ari -n -f %.0f $ddE x  $ethr | tr -d '-'`

if ( "$dE" != 0 ) then
  if ( -e RelaxConvergent ) rm RelaxConvergent
  goto out0
endif

#set pwdc = $www/$dir/$d
set pwdc = $www
set tmpdir =  $SCRATCH/`echo $pwdc | sed "s|/|_|g;s|_global_project_projectdirs_m891_yiwang62|prj|g"`
set ibzkpt =  $tmpdir/IBZKPT
set toszicar =  $tmpdir/OSZICAR

set  sameosz = 9
if ( -e $toszicar ) then
if ( ! -z $toszicar ) then
  set sameosz = "`diff $toszicar $oszicar | wc -l`"
  
endif
endif

set nk = 0
if ( "$sameosz" == 0 ) then
if ( -e $ibzkpt ) then
if ( ! -z $ibzkpt ) then
  set nk = `awk '{print $4}' $ibzkpt | awk '{total += $0} END{print total}'`
  set natom = `head -7 $tmpdir/POSCAR | tail -1 | awk '{ nn=0; for (i=1; i<=NF; i++) nn += $i; print nn;}'`
  @ nk = "$nk" * "$natom"
endif
endif
endif


if ( ! $?outcar ) then
  if ( "$nk" != 0 ) then
    echo "RelaxConvergent $nk"
    echo "RelaxConvergent $nk" > RelaxConvergent
  else if ( -e RelaxConvergent ) then
    cat RelaxConvergent
  else
    echo "RelaxConvergent $nk"
    echo "RelaxConvergent $nk" > RelaxConvergent
  endif
  exit
endif

if ( ! -e $outcar ) then
  echo "********ERROR: `pwd`$outcar not EXIST"
  exit
endif
if ( -z $outcar ) then
  echo "********ERROR: `pwd`$outcar is empty"
  exit
endif

set zipped = `file $outcar | grep "gzip compressed data"`

if ( "$zipped" != "" ) then
  set enmax = `zcat $outcar | grep ENMAX | tr -d ';' | awk '{print $3}' | sort -n | tail -1`
  set encut = `zcat $outcar | grep ENCUT | tr -d ';' | awk '{print $3}' | sort -n | tail -1`
  set ismear = `zcat $outcar | grep SMEAR | tr -d ';' | awk '{print $3}' | sort -n | tail -1`
else
  set enmax = `grep ENMAX $outcar | tr -d ';' | awk '{print $3}' | sort -n | tail -1`
  set encut = `grep ENCUT $outcar | tr -d ';' | awk '{print $3}' | sort -n | tail -1`
  set ismear = `grep SMEAR $outcar | tr -d ';' | awk '{print $3}' | sort -n | tail -1`
endif

set high = `ari -n -f %.0f $encut - $enmax / $enmax x 100`
if ( ("$high" >= "25") && ("$ismear" == "-5") ) then
  if ( "$nk" != 0 ) then
    echo "StaticConvergent $nk"
    echo "StaticConvergent $nk" > StaticConvergent
  else if ( -e StaticConvergent ) then
    cat StaticConvergent
  else
    echo "StaticConvergent $nk"
    echo "StaticConvergent $nk" > StaticConvergent
  endif
else if ( -e StaticConvergent ) then
  rm StaticConvergent
endif

exit

out0:

echo ""
